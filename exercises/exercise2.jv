// Exercise 2

// Build an automated data pipeline for the following source:
// https://www.govdata.de/web/guest/suchen/-/details/stadt-neuss-baumpflanzungen-2023
// direct link to CSV: https://opendata.rhein-kreis-neuss.de/api/v2/catalog/datasets/stadt-neuss-herbstpflanzung-2023/exports/csv
// Do NOT use a local file, always take the data from the link above

// Goal

// Keep only valid data we are interested in
// Define types of values and constraints for them
// We are only interested in “Vogelsang”, valid stadtteil start with "Vogelsang"
// "id" contains geopoints with the following pattern:
//              {geo-coordinate 1}, {geo-coordinate 2}
//              a geo-coordinate is defined as {1-3 numbers}.{numbers}


pipeline TreesPipeline{

    TreesHttpExtractor
        ->TreesTextFileInterpreter
        ->TreesCSVInterpreter
        ->TreeColumnDeleter
        ->TreesTableInterpreter
        ->TreesLoader;


    block TreesHttpExtractor oftype HttpExtractor {
        url: "https://opendata.rhein-kreis-neuss.de/api/v2/catalog/datasets/stadt-neuss-herbstpflanzung-2023/exports/csv";
    }

    block TreesTextFileInterpreter oftype TextFileInterpreter { }

    block TreesCSVInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }

    block TreeColumnDeleter oftype ColumnDeleter {
        delete: [column E];
    }

    constraint Vogelsangregex oftype RegexConstraint {

        regex: /^Vogelsang/;
    }

    valuetype vogelsang oftype text {
        constraints: [
            Vogelsangregex,
        ];
    }

    constraint GeoPointsRegex oftype RegexConstraint {
        regex: /^\d{1,3}\.\d+,\s\d{1,3}\.\d+$/;
    }

    valuetype geopoints oftype text {
        constraints: [
            GeoPointsRegex,
        ];
    }

    block TreesTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "lfd_nr" oftype integer,
            "stadtteil" oftype vogelsang,
            "standort" oftype text,
            "baumart_botanisch" oftype text,
            "id" oftype geopoints,
            "baumfamilie" oftype text
        ];
    }

    block TreesLoader oftype SQLiteLoader {
        table: "trees";
        file: "./trees.sqlite";
    }

}


